ALTER TABLE [dbo].cy_SurveyResponses ADD
	UserGuid uniqueidentifier NULL 
GO

UPDATE cy_SurveyResponses
SET	UserGuid = (SELECT TOP 1 cy_Users.UserGuid FROM cy_Users WHERE cy_Users.UserID = cy_SurveyResponses.UserID)
GO

ALTER TABLE [dbo].cy_SurveyResponses DROP COLUMN UserId
	
GO


SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER OFF
GO


ALTER PROCEDURE [dbo].[cy_SurveyResults_Select]

/*
Author:   			Rob Henry
Created: 			18/09/2007
Last Modified: 		2008-01-19
*/

@ResponseGuid uniqueidentifier

AS


SELECT
sq.QuestionText,
qa.Answer,
sp.PageTitle,
sq.QuestionGuid,
sr.ResponseGuid

FROM
	cy_Surveys s
JOIN
	cy_SurveyResponses sr
ON
	s.SurveyGuid = sr.SurveyGuid

JOIN
	cy_SurveyPages sp
ON
	sr.SurveyGuid = sp.SurveyGuid

JOIN
	cy_SurveyQuestions sq
ON
	sp.PageGuid = sq.PageGuid

LEFT OUTER JOIN
	cy_SurveyQuestionAnswers qa
ON
	sq.QuestionGuid = qa.QuestionGuid
	AND qa.ResponseGuid = @ResponseGuid

WHERE
sr.ResponseGuid = @ResponseGuid
AND
sr.Complete = 1
AND
sp.PageEnabled = 1

ORDER BY
sp.PageOrder, sq.QuestionOrder

		
GO

